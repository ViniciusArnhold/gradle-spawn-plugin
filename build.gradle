apply plugin: 'idea'
apply plugin: 'groovy'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy:1.8.9'
    compile gradleApi()
    testCompile 'org.spockframework:spock-core:0.7-groovy-1.8'
}

group = 'com.wiredforcode'
archivesBaseName = 'gradle-spawn-plugin'
version = '0.5.2'

apply plugin: 'distribution'

distributions {
    main {
        baseName = archivesBaseName
        contents {
            from { libsDir }
        }
    }
}

ext {
    bintrayBaseUrl = 'https://api.bintray.com/maven'
    bintrayUsername = 'vermeulen-mp'
    bintrayRepository = 'gradle-plugins'
    bintrayPackage = 'gradle-spawn-plugin'
}

apply plugin: 'maven-publish'

task sourceJar(type: Jar) {
   from sourceSets.main.allSource
   classifier = 'source'
}

publishing {
    publications {
        plugin(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }

            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'Gradle Spawn plugin')
                root.appendNode('description', 'Gradle plugin for spawning and killing command line processes.')
                root.appendNode('inceptionYear', '2014')

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The Apache Software License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                license.appendNode('distribution', 'repo')

                def developers = root.appendNode('developers')

                def developer1 = developers.appendNode('developer')
                developer1.appendNode('id', 'vermeulen.mp')
                developer1.appendNode('name', 'Marco Vermeulen')
                developer1.appendNode('email', 'vermeulen.mp@gmail.com')

                def developer2 = developers.appendNode('developer')
                developer2.appendNode('id', 'tomaslin')
                developer2.appendNode('name', 'Tomas Lin')
                developer2.appendNode('email', 'tomaslin+bintray@gmail.com')
            }

        }
    }

    repositories {
        maven {
            name 'Bintray'
            url "$bintrayBaseUrl/$bintrayUsername/$bintrayRepository/$bintrayPackage"

            credentials {
                username = System.getenv('BINTRAY_USERNAME') ?: 'invalid_user'
                password = System.getenv('BINTRAY_API_KEY') ?: 'invalid_key'
            }
        }
    }
}
